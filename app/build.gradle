apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "com.google.protobuf"

android {
    compileSdkVersion(appTargetSdk)
    buildToolsVersion(appBuildTool)
    defaultConfig {
        applicationId(appPackageName)
        minSdkVersion(appMinSdk)
        targetSdkVersion(appTargetSdk)
        versionCode(buildTime().toInteger())
        versionName(appVersionName)
        ndk { abiFilters(ndkLibs) }
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled = true
    }

    buildTypes {
        release {
            minifyEnabled = true
            zipAlignEnabled = true
            shrinkResources = true
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
        }
        debug {
            jniDebuggable = true
            renderscriptDebuggable = true
            crunchPngs = false
            ext.alwaysUpdateBuildId = false
        }
    }
    /*applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                outputFileName = "Demo_${buildTime()}.apk"
            }
        }
    }*/
    dependenciesInfo {
        includeInApk = false
        includeInBundle = false
    }
    buildFeatures {
        dataBinding = true
        viewBinding = true
    }
    sourceSets {
        main {
            proto {
                srcDir "src/main/proto"
            }
        }
    }
    lintOptions {
        abortOnError = true
    }
    compileOptions {
        sourceCompatibility(JavaVersion.VERSION_1_8)
        targetCompatibility(JavaVersion.VERSION_1_8)
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

protobuf {
    protoc {
        artifact = libs["protoc"]
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

dependencies {
    implementation(fileTree(dir: "libs", include: libTypes))
    implementation project(baseModule)
}